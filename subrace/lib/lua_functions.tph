DEFINE_PATCH_MACRO FC_UPDATE_MENU_ELEMENT BEGIN
	INNER_PATCH_SAVE ~menu_menu~ ~%menu_menu%~ BEGIN
		DELETE_BYTES  offset_start offset_end - offset_start
		SET len = STRING_LENGTH ~%element_text%~
		INSERT_BYTES offset_start len
		WRITE_ASCIIE offset_start ~%element_text%~
	END
END

DEFINE_PATCH_MACRO FC_INSERT_MENU_ELEMENT BEGIN
	INNER_PATCH_SAVE ~menu_menu~ ~%menu_menu%~ BEGIN
		SPRINT ~element_text~ ~%LNL%%TAB%%element_text%~
		SET len = STRING_LENGTH ~%element_text%~
		INSERT_BYTES offset_end len
		WRITE_ASCIIE offset_end ~%element_text%~
	END
END

DEFINE_PATCH_FUNCTION FC_REPLACE_ELEMENT_ACTION
	INT_VAR
		replace = 1 //0 - edit action, 1 - replace action
		position = 0 //0 - top of action_check, 1 - bottom
	STR_VAR
		element_text = ""
		action_type = "action"
		action_string = ""
		action_check = ""
	RET
		element_text
BEGIN
	PATCH_IF ( ~%action_string%~ STR_CMP ~~ ) BEGIN
		PATCH_IF replace BEGIN
			SPRINT ~action_text~ ~%action_type%%LNL%%TAB%%TAB%"%LNL%%TAB%%TAB%%TAB%%action_string%%LNL%%TAB%%TAB%"~
		END ELSE BEGIN
			INNER_PATCH ~%element_text%~ BEGIN
				SET offs = INDEX_BUFFER(~%action_type%[ %TAB%%WNL%%LNL%%MNL%]*"~)
				PATCH_IF ( offs >= 0 ) BEGIN
					SET offs1 = INDEX_BUFFER(~"~ offs) + 1
					SET offs2 = INDEX_BUFFER(~"~ offs1)
					READ_ASCII offs1 action_text ( offs2 - offs1 )
					INNER_PATCH_SAVE ~action_text~ ~%action_text%~ BEGIN
						SET offs1 = INDEX_BUFFER(~%action_check%~)
						PATCH_IF ( offs1 >= 0 ) BEGIN
							REPLACE_EVALUATE ~^\([ %TAB%]*\)\(.*\)\(%action_check%\)\(.*\)$~
							BEGIN
								PATCH_IF position BEGIN
									SPRINT fline ~~
									SPRINT lline ~%LNL%%MATCH1%%action_string%~
								END ELSE BEGIN
									PATCH_IF ( ~%MATCH1%~ STR_EQ ~~ ) BEGIN
										SPRINT nl ~%LNL%%TAB%%TAB%%TAB%~
										SPRINT ~MATCH1~ ~%TAB%%TAB%%TAB%~
									END ELSE BEGIN
										SPRINT nl ~%MATCH1%~
									END
									SPRINT fline ~%nl%%action_string%%LNL%~
									SPRINT lline ~~
								END
							END
							~%fline%%MATCH1%%MATCH2%%MATCH3%%MATCH4%%lline%~
						END
					END
					SPRINT ~action_text~ ~%action_type%%LNL%%TAB%%TAB%"%action_text%"~
				END ELSE BEGIN
					SPRINT ~action_text~ ~%action_type%%LNL%%TAB%%TAB%"%LNL%%TAB%%TAB%%TAB%%action_string%%LNL%%TAB%%TAB%"~
				END
			END
		END
		SET lennew = STRING_LENGTH ~%element_text%~
		INNER_PATCH_SAVE ~element_text~ ~%element_text%~ BEGIN
			SET offs = INDEX_BUFFER(~%action_type%[ %TAB%%WNL%%LNL%%MNL%]*"~)
			PATCH_IF ( offs >= 0 ) BEGIN
				SET offs1 = INDEX_BUFFER(~"~ offs) + 1
				SET offs2 = INDEX_BUFFER(~"~ offs1) + 1
				DELETE_BYTES offs offs2 - offs
			END ELSE BEGIN
				SET offs = lennew - 2
			END
			SET len = STRING_LENGTH ~%action_text%~
			INSERT_BYTES offs len
			WRITE_ASCIIE offs  ~%action_text%~
		END
	END
END

DEFINE_PATCH_FUNCTION FC_GETSET_ELEMENT_AREA
	INT_VAR
		set_area = 0 //0 get, 1 - set
		coord_x = 0
		coord_y = 0
		width = 0
		height = 0
	STR_VAR
		element_text = ""
	RET
		coord_x coord_y width height element_text
BEGIN
	SPRINT nl ~~
	SET startoffs = 0
	SET delete = 0
	INNER_PATCH ~%element_text%~ BEGIN
		READ_ASCII 0 tp (4)
		PATCH_IF ( ~%tp%~ STR_EQ ~list~ ) BEGIN
			SET len = BUFFER_LENGTH - 2
			SET offs = RINDEX_BUFFER(~}~ len)
			PATCH_IF ( offs >= 0 ) BEGIN SET startoffs = offs END
		END
		SET offs = INDEX_BUFFER(~area[ %TAB%]+[0-9]+[ %TAB%]+[0-9]+[ %TAB%]+[0-9]+[ %TAB%]+[0-9]+~ startoffs)
		PATCH_IF set_area BEGIN
			PATCH_IF ( offs >= 0 ) BEGIN
				SET offs1 = INDEX_BUFFER(~[%WNL%%LNL%%MNL%]~ offs)
				SET delete = offs1 - offs
			END ELSE BEGIN
				SET offs = INDEX_BUFFER(~{~) + 1
				SPRINT nl ~%LNL%%TAB%%TAB%~
			END
		END ELSE BEGIN
			PATCH_IF ( offs >= 0 ) BEGIN
				SET offs1 = INDEX_BUFFER(~[%WNL%%LNL%%MNL%]~ offs)
				READ_ASCII offs areas ( offs1 - offs )
				INNER_PATCH ~%areas%~ BEGIN
					REPLACE_EVALUATE ~area[ %TAB%]+\([0-9]+\)[ %TAB%]+\([0-9]+\)[ %TAB%]+\([0-9]+\)[ %TAB%]+\([0-9]+\)~
					BEGIN
						SET coord_x = ~%MATCH1%~
						SET coord_y = ~%MATCH2%~
						SET width 	= ~%MATCH3%~
						SET height 	= ~%MATCH4%~
					END
					~area %MATCH1% %MATCH2% %MATCH3% %MATCH4%~
				END
			END
		END
	END
	PATCH_IF set_area BEGIN
		INNER_PATCH_SAVE ~element_text~ ~%element_text%~ BEGIN
			PATCH_IF ( delete > 0 ) BEGIN
				DELETE_BYTES offs delete
			END
			SPRINT ~new_area~ ~%nl%area%TAB%%coord_x% %coord_y% %width% %height%~
			SET len = STRING_LENGTH ~%new_area%~
			INSERT_BYTES offs len
			WRITE_ASCIIE offs ~%new_area%~
		END
	END
END
